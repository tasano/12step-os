OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
    ramall(rwx) : o = 0xFFBF20, l = 0x004000    /* 16KB */
    softvec(rw) : o = 0xFFBF20, l = 0x000040    /* top of RAM */
    ram(rwx)    : o = 0xFFC020, l = 0x003F00
    stack(rw)   : o = 0xFFFF00, l = 0x000000    /* end of RAM */
}

SECTIONS
{
    .softvec : {
        _softvec = . ;
    } > softvec

    .text : {
        _text_start = . ;
        *(.text)
        _etext = . ;
    } > ram

    .rodata : {
        _rodata_start = . ;
        *(.strings)
        *(.rodata)
        *(.rodata.*)
        _erodata = . ;
    } > ram

    .data : {
        _data_start = . ;
        *(.data)
        _edata = . ;
    } > ram

    .bss : {
        _bss_start = . ;
        *(.bss)
        *(COMMON)
        _ebss = . ;
    } > ram

    . = ALIGN(4);
    _end = . ;

    .stack : {
        _stack = . ;
    } > stack
}
